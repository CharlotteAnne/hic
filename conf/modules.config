process {

  //Default
  publishDir = [
    path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
    mode: 'copy',
    saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
  ]

  // PREPARE_GENOME
  withName: 'BOWTIE2_BUILD' {
    publishDir = [
      path: { "${params.outdir}/genome/bowtie2" },
      mode: 'copy',
      enabled: params.save_reference
    ]
  }

  withName: 'GET_CHROMSIZE' {
    publishDir = [
      path: { "${params.outdir}/genome" },
      mode: 'copy',
      enabled: params.save_reference
    ]
  }

  withName: 'GET_RESTRICTION_FRAGMENTS' {
    publishDir = [
      path: { "${params.outdir}/genome" },
      mode: 'copy',
      enabled: params.save_reference
    ]
  }

  // HICPRO
  withName:'BOWTIE2_ALIGN' {
    ext.prefix = { "${meta.id}_${meta.mates}" }
    ext.args = params.bwt2_opts_end2end ?: ''
  }

  withName:'BOWTIE2_ALIGN_TRIMMED' {
    ext.prefix = { "${meta.id}_${meta.mates}_trimmed" }
    ext.args = params.bwt2_opts_trimmed ?: ''
  }

  withName: 'COMBINE_MATES' {
    ext.args = [
      "-t",
      params.keep_multi ? "--multi" : "",
      params.min_mapq ? "-q ${params.min_mapq}" : ""
    ].join(' ').trim()
  }

  withName: 'GET_VALID_INTERACTION' {
    ext.args = [
      params.min_cis_dist > 0 ? " -d ${params.min_cis_dist}" : '',
      params.min_insert_size > 0 ?  " -s ${params.min_insert_size}" : '',
      params.max_insert_size > 0 ? " -l ${params.max_insert_size}" : '',
      params.min_restriction_fragment_size > 0 ? " -t ${params.min_restriction_fragment_size}" : '',
      params.max_restriction_fragment_size > 0 ? " -m ${params.max_restriction_fragment_size}" : '',
      params.save_interaction_bam ? " --sam" : ''
    ].join(' ').trim()
  }
}
